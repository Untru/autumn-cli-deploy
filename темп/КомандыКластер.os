#Использовать v8rac
&Пластилин Перем МенеджерОпций; //Класс для хранения опций выполнения

Перем Кластер; //Класс v8rac УправлениеКластером

&Логер
Перем Лог;

&Желудь
Процедура ПриСозданииОбъекта() Экспорт

КонецПроцедуры

Процедура Инициализировать() Экспорт
	
	Кластер = Новый УправлениеКластером;

	ПутьКСерверу = МенеджерОпций.ВернутьСвойство("PathServer1с"); 
	Кластер.УстановитьКластер(ПутьКСерверу);
	Кластер.ИспользоватьВерсию("8.3");
	
	Если МенеджерОпций.ЗапускатьRas Тогда
		ВключитьRAS();
	КонецЕсли;

	Кластер.Подключить();
	
	ИдентификаторИнформационнойбаза = Кластер.НайтиИнформационнуюБазу(МенеджерОпций.ВернутьСвойство("NameDB"));
	МенеджерОпций.ИдентификаторИнформационнойбаза = ИдентификаторИнформационнойбаза;
	
	Кластер.УстановитьАвторизациюИнформационнойБазы(ИдентификаторИнформационнойбаза
		, МенеджерОпций.ВернутьСвойство("UsrLogin1c"), МенеджерОпций.ВернутьСвойство("UsrPswd1c"));
	
	
КонецПроцедуры


// Устанавливает авторизацию на кластере 1С
//
// Параметры:
//   Пользователь - Строка - администратор кластера
//   Пароль - Строка - пароль администратора кластера
//
Процедура УстановитьАвторизациюКластера(Знач Пользователь, Знач Пароль = "") Экспорт
	
	Кластер.УстановитьАвторизациюКластера(Пользователь, Пароль);

КонецПроцедуры

Процедура ВключитьRAS()
	
	ИмяПроцесса = "ras";
	МассивПроцессов = НайтиПроцессыПоИмени(ИмяПроцесса);
	
	Для Каждого Процесс Из МассивПроцессов Цикл
		Процесс.Завершить();
	КонецЦикла;
	
	//TODO Уйти от версии
	EXERAS = "C:\\Program Files\\1cv8\\8.3.25.1336\\bin\\ras.exe";
	PathServer1с = МенеджерОпций.ВернутьСвойство("PathServer1с");
	
	Команда = Новый Команда;
	Команда.УстановитьСтрокуЗапуска(ОбщегоНазначения.ОбернутьВКавычки(EXERAS) + " cluster " + PathServer1с);
	Команда.Исполнить();
	
КонецПроцедуры

Процедура ЗаблокироватьБазу(ОтключатьСеансы = Истина) Экспорт
	
	ДатаОкончанияБлокировки = ТекущаяДата() + 60 * 60 * 2;
	КодДоступа = МенеджерОпций.ВернутьСвойство("AccessCode");
	Лог.Информация("КодДоступа " + КодДоступа);
	Если Не ЗначениеЗаполнено(КодДоступа) Тогда
		КодДоступа = "Обновление";
	КонецЕсли;
	Кластер.БлокировкаИнформационнойБазы(МенеджерОпций.ИдентификаторИнформационнойбаза, 
		"База заблокирована для обновления", КодДоступа,, ДатаОкончанияБлокировки
	);

	Если ОтключатьСеансы Тогда
		Кластер.ОтключитьСеансыИнформационнойБазы(МенеджерОпций.ВернутьСвойство("NameDB"));
		Кластер.ОтключитьСоединенияИнформационнойБазы(МенеджерОпций.ВернутьСвойство("NameDB"));
	КонецЕсли;
	
КонецПроцедуры

Процедура РазблокироватьБазу() Экспорт
	
	Лог.Информация(МенеджерОпций.ИдентификаторИнформационнойбаза);
	Кластер.СнятьБлокировкуИнформационнойБазы(МенеджерОпций.ИдентификаторИнформационнойбаза, Истина);
	
КонецПроцедуры