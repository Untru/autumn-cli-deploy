Перем _КомандыКластер;
Перем _КомандыПараметры;
Перем _КомандыКонфигуратор;
Перем _КомандыГИТ;

&Пластилин Перем КомандыГИТ Экспорт;
&Пластилин Перем КомандыКонфигуратор Экспорт;
&Пластилин Перем КомандыПараметры Экспорт;
&Пластилин Перем КомандыКластер Экспорт;

&Логер
Перем Лог;

&Опция(Имя = "PathRepository", Описание = "Путь репозитория")
Перем PathRepository;

&Опция(Имя = "NameDB", Описание = "Имя информационной базы")
Перем NameDB;

&Опция(Имя = "UsrLogin1c", Описание = "Имя пользователя 1с")
Перем UsrLogin1c;

&Опция(Имя = "UsrPswd1c", Описание = "Пароль пользователя 1с")
Перем UsrPswd1c;

&Опция(Имя = "PathServer1с", Описание = "Путь к серверу 1с")
Перем PathServer1с;

&Опция(Имя = "NumberPlatform", Описание = "Номер версии платформы 1с")
Перем NumberPlatform;

&Опция(Имя = "TypeDB", Описание = "Тип СУБД")
Перем TypeDB;

&Опция(Имя = "UsrLoginDB", Описание = "Имя пользователя СУБД")
Перем UsrLoginDB;

&Опция(Имя = "UsrPswdDB", Описание = "Пароль пользователя СУБД")
Перем UsrPswdDB;

&Опция(Имя = "PathServerDB", Описание = "Путь к серверу СУБД")
Перем PathServerDB;

&Опция(Имя = "RunRas", Описание = "Запускать RAS")
Перем RunRas;

&Опция(Имя = "MethodLoadDB", Описание = "ibcmd / designer")
Перем MethodLoadDB;

&Опция(Имя = "LoadDB", Описание = "Загрузка базы")
Перем LoadDB;

&Опция(Имя = "PathRelease", Описание = "Путь к релизу")
Перем PathRelease;

&Опция(Имя = "NameRelease", Описание = "Название релиза")
Перем NameRelease;

&Опция(Имя = "NamesDataProcessors", Описание = "Имена внешних обработок")
Перем NamesDataProcessors;

&Опция(Имя = "PathStorage", Описание = "Путь к хранилищу")
Перем PathStorage;

&Опция(Имя = "UserStorage", Описание = "Пользователь хранилища")
Перем UserStorage;

&Опция(Имя = "NameDBWeb", Описание = "Имя опубликованной базы")
Перем NameDBWeb;

&Опция(Имя = "ServerDBWeb", Описание = "Сервер опубликованной базы")
Перем ServerDBWeb;

&Опция(Имя = "PathStorageAelita", Описание = "Путь к хранилищу Aelita")
Перем PathStorageAelita;

&Опция(Имя = "UserStorageAelita", Описание = "Пользователь хранилища Aelita")
Перем UserStorageAelita;

&Опция(Имя = "PathStorageToolsWorld", Описание = "Путь к хранилищу Toolsworld")
Перем PathStorageToolsworld;

&Опция(Имя = "UserStorageToolsWorld", Описание = "Пользователь хранилища Toolsworld")
Перем UserStorageToolsworld;

&КомандаПриложения(Имя = "BuildRelease")
Процедура ПриСозданииОбъекта()
		
КонецПроцедуры

&ВыполнениеКоманды
Процедура ОбработатьКоманду() Экспорт

	НастройкиВыполнения = НастройкиВыполнения();
	
	ИнициализироватьНастройки(НастройкиВыполнения, RunRas);
	ИнициализироватьКластер();
	ИнициализироватьГит();
	ИнициализироватьКонфигуратор();

	_КомандыГИТ.ПерейтиНаВеткуМейн();

	ПутьОбработокРепо = PathRepository + "\build\epf";

	КаталогБилд = PathRepository + "\build";

	файловыеОперации.ОбеспечитьПустойКаталог(ПутьОбработокРепо);
	КаталогРелиза = СтрШаблон("%1/%2", PathRelease, NameRelease);
	файловыеОперации.ОбеспечитьКаталог(КаталогРелиза);

	КаталогОбработокРелиза = СтрШаблон("%1/%2", КаталогРелиза, "Обработки");
	файловыеОперации.ОбеспечитьКаталог(КаталогОбработокРелиза);

	КаталогКонфигурацииБилд = КаталогБилд + "/cf";
	файловыеОперации.ОбеспечитьКаталог(КаталогКонфигурацииБилд);

	КаталогРасширенийБилд = КаталогБилд + "/cfe";
	файловыеОперации.ОбеспечитьКаталог(КаталогРасширенийБилд);

	КаталогКонфигурацииРелиза = КаталогРелиза + "/cf";
	файловыеОперации.ОбеспечитьКаталог(КаталогКонфигурацииРелиза);

	КаталогРасширенийРелиза = КаталогРелиза + "/cfe";
	файловыеОперации.ОбеспечитьКаталог(КаталогРасширенийРелиза);		

	_КомандыКластер.ЗаблокироватьБазу();

	Если LoadDB Тогда
		_КомандыКонфигуратор.ОтключитьсяОтХранилища();

		//Обновить конфигурацию из файлов
		Лог.Информация(" Старт: Обновление базы " + ТекущаяДата());
		_КомандыКонфигуратор.ОбновитьБазуИзФайлов();
		Лог.Информация(" Конец: Обновление базы " + ТекущаяДата());
	КонецЕсли;

	//Загрузить расширения	
	Лог.Информация(" Старт: загрузка расширений " + ТекущаяДата());
	_КомандыКонфигуратор.ОтключитьсяОтХранилищаРасширений();
	_КомандыКонфигуратор.ЗагрузитьРасширенияИзФайлов();
	Лог.Информация(" Конец: загрузка расширений " + ТекущаяДата());
	
	//Скомпилировтаь обработки
	Лог.Информация(" Старт: Компиляция обработок " + ТекущаяДата());
	_КомандыКонфигуратор.СобратьОбработки();
	Лог.Информация(" Конец: Компиляция обработок " + ТекущаяДата());

	_КомандыКластер.РазблокироватьБазу();

	Лог.Информация(" Старт: Перенос обработок в базу " + ТекущаяДата());
	_КомандыКонфигуратор.ОбновитьОбработкиВКонфигурации();
	Лог.Информация(" Конец: Перенос обработок в базу " + ТекущаяДата());

	
	Лог.Информация(" Старт: Перенос обработок в каталог релиза " + ТекущаяДата());
	файловыеОперации.КопироватьСодержимоеКаталога(ПутьОбработокРепо, КаталогОбработокРелиза);
	файловыеОперации.ОбеспечитьПустойКаталог(ПутьОбработокРепо);
	Лог.Информация(" Конец: Перенос обработок в каталог релиза " + ТекущаяДата());			

	Если LoadDB Тогда
		Лог.Информация(" Старт: Перенос конфигурации в каталог релиза " + ТекущаяДата());
		_КомандыКонфигуратор.ВыгрузитьКонфигурациюВФайл(КаталогКонфигурацииРелиза + "/ГотоваяКонфигурацияВПрод.cf");

		Лог.Информация(" Конец: Перенос конфигурации в каталог релиза " + ТекущаяДата());

	КонецЕсли;
	
	Лог.Информация(" Старт: Перенос расширений в каталог релиза " + ТекущаяДата());
	_КомандыКонфигуратор.ВыгрузитьРасширениеВФайл(КаталогРасширенийРелиза + "/ToolsWorld.cfe", "ToolsWorld");
	Попытка
		_КомандыКонфигуратор.ВыгрузитьРасширениеВФайл(КаталогРасширенийРелиза + "/Aelita.cfe", "Aelita");	
	Исключение
		Лог.Информация(ОписаниеОшибки());
	КонецПопытки;

	Лог.Информация(" Конец: Перенос расширений в каталог релиза " + ТекущаяДата());	

	Если LoadDB Тогда
		_КомандыКонфигуратор.ПодключитьсяКХранилищу();

		Лог.Информация(" Старт: Обновление хранилища продуктива " + ТекущаяДата());
		_КомандыКонфигуратор.ОбновитьХранилищеПродуктива(КаталогКонфигурацииРелиза + "/ГотоваяКонфигурацияВПрод.cf",
			PathRepository + "/ФайлНастроек.xml");
		Лог.Информация(" Конец: Обновление хранилища продуктива " + ТекущаяДата());
	КонецЕсли;

	Лог.Информация(" Старт: Обновление хранилища расширений " + ТекущаяДата()); 
	_КомандыКонфигуратор.ПодключитьсяКХранилищуРасширений();
	_КомандыКонфигуратор.ОбновитьХранилищеПродуктиваРасширений(КаталогРасширенийРелиза,
		PathRepository + "/ФайлНастроек.xml");
		
	Лог.Информация(" Старт: Обновление хранилища расширений " + ТекущаяДата());

	Лог.Информация("Обработка завершена");	
	
КонецПроцедуры

Функция НастройкиВыполнения()

	НастройкиВыполнения = Новый Структура;
	НастройкиВыполнения.Вставить("PathRepository", PathRepository);
	НастройкиВыполнения.Вставить("NameDB", NameDB);
	НастройкиВыполнения.Вставить("UsrLogin1c", UsrLogin1c);
	НастройкиВыполнения.Вставить("UsrPswd1c", UsrPswd1c);
	НастройкиВыполнения.Вставить("PathServer1с", PathServer1с);
	НастройкиВыполнения.Вставить("NumberPlatform", NumberPlatform);
	НастройкиВыполнения.Вставить("TypeDB", TypeDB);
	НастройкиВыполнения.Вставить("UsrLoginDB", UsrLoginDB);
	НастройкиВыполнения.Вставить("UsrPswdDB", UsrPswdDB);
	НастройкиВыполнения.Вставить("PathServerDB", PathServerDB);
	НастройкиВыполнения.Вставить("RunRas", RunRas);
	НастройкиВыполнения.Вставить("MethodLoadDB", MethodLoadDB);
	НастройкиВыполнения.Вставить("LoadDB", LoadDB);	
	НастройкиВыполнения.Вставить("PathRelease", PathRelease);
	НастройкиВыполнения.Вставить("NameRelease", NameRelease);
	НастройкиВыполнения.Вставить("NamesDataProcessors", NamesDataProcessors);		
	НастройкиВыполнения.Вставить("PathStorage", PathStorage);
	НастройкиВыполнения.Вставить("UserStorage", UserStorage);
	НастройкиВыполнения.Вставить("NameDBWeb", NameDBWeb);
	НастройкиВыполнения.Вставить("ServerDBWeb", ServerDBWeb);
	НастройкиВыполнения.Вставить("PathStorageAelita", PathStorageAelita);
	НастройкиВыполнения.Вставить("UserStorageAelita", UserStorageAelita);
	НастройкиВыполнения.Вставить("PathStorageToolsWorld", PathStorageToolsWorld);
	НастройкиВыполнения.Вставить("UserStorageToolsWorld", UserStorageToolsWorld);		

	Возврат НастройкиВыполнения;

КонецФункции

#Область Инициализация

Процедура ИнициализироватьКластер(Идентификатор = "default") Экспорт
	
	_КомандыКластер = КомандыКластер;
	_КомандыКластер._КомандыПараметры = _КомандыПараметры;

	_КомандыКластер.Инициализировать(Идентификатор);
	
КонецПроцедуры


Процедура ИнициализироватьКонфигуратор(Идентификатор = "default") Экспорт
	
	_КомандыКонфигуратор = КомандыКонфигуратор;
	_КомандыКонфигуратор._КомандыПараметры = _КомандыПараметры;

	_КомандыКонфигуратор.Инициализировать(Идентификатор);
	
КонецПроцедуры

Процедура ИнициализироватьГит(Идентификатор = "default") Экспорт
	
	_КомандыГИТ = КомандыГИТ;
	_КомандыГИТ._КомандыПараметры = _КомандыПараметры;
	_КомандыГИТ.Инициализировать(Идентификатор);

КонецПроцедуры

Процедура ИнициализироватьНастройки(НастройкиВыполнения, ЗапускатьRas) Экспорт
	
	_КомандыПараметры = КомандыПараметры;
	_КомандыПараметры.Параметры = НастройкиВыполнения;

	_КомандыПараметры.ЗапускатьRas = ЗапускатьRas;
	_КомандыПараметры.Инициализировать();
	
КонецПроцедуры

#КонецОбласти