Перем _КомандыКластер;
Перем _КомандыПараметры;
Перем _КомандыКонфигуратор;
Перем _КомандыГИТ;

&Пластилин Перем КомандыГИТ Экспорт;
&Пластилин Перем КомандыКонфигуратор Экспорт;
&Пластилин Перем КомандыПараметры Экспорт;
&Пластилин Перем КомандыКластер Экспорт;

&Опция(Имя = "PathRepository", Описание = "Путь репозитория")
Перем PathRepository;

&Опция(Имя = "NameDB", Описание = "Имя информационной базы")
Перем NameDB;

&Опция(Имя = "UsrLogin1c", Описание = "Имя пользователя 1с")
Перем UsrLogin1c;

&Опция(Имя = "UsrPswd1c", Описание = "Пароль пользователя 1с")
Перем UsrPswd1c;

&Опция(Имя = "PathServer1с", Описание = "Путь к серверу 1с")
Перем PathServer1с;

&Опция(Имя = "NumberPlatform", Описание = "Номер версии платформы 1с")
Перем NumberPlatform;

&Опция(Имя = "TypeDB", Описание = "Тип СУБД")
Перем TypeDB;

&Опция(Имя = "ServerDB", Описание = "ИМЯ сервера SQL")
Перем ServerDB;

&Опция(Имя = "UsrLoginDB", Описание = "Имя пользователя СУБД")
Перем UsrLoginDB;

&Опция(Имя = "UsrPswdDB", Описание = "Пароль пользователя СУБД")
Перем UsrPswdDB;

&Опция(Имя = "PathServerDB", Описание = "Путь к серверу СУБД")
Перем PathServerDB;

&Опция(Имя = "RunRas", Описание = "Запускать RAS")
Перем RunRas;

&Опция(Имя = "Extensions", Описание = "Имена расширений конфигурации")
Перем Extensions;

&Опция(Имя = "TextCommit", Описание = "Текст коммита")
Перем TextCommit;

&Опция(Имя = "UrlRemoteRepository", Описание = "Ссылка на Удаленный репозиторий")
Перем UrlRemoteRepository;

&Опция(Имя = "TokenConnectionGitLab", Описание = "Токен подключения к гитлаб")
Перем TokenConnectionGitLab;

&Опция(Имя = "IdProject", Описание = "ИД проекта Гит лаб (отдельно ВМС, отдельно ЕРП, отдельно ТМС)")
Перем IdProject;

&Опция(Имя = "MethodLoadDB", Описание = "ibcmd / designer")
Перем MethodLoadDB;

&Опция(Имя = "LoadDB", Описание = "Обновлять базу из файлов")
Перем LoadDB;

&Опция(Имя = "CommitAuthor", Описание = "Автор, от которого отправляется коммит")
Перем CommitAuthor;

&КомандаПриложения(Имя = "commit")
Процедура ПриСозданииОбъекта()
	
КонецПроцедуры

&ВыполнениеКоманды
Процедура ОбработатьКоманду() Экспорт
	
	НастройкиВыполнения = НастройкиВыполнения();
	Идентификатор = "default";
	ИнициализироватьНастройки(НастройкиВыполнения, RunRas);
	ИнициализироватьКластер();
	ИнициализироватьГит();
	ИнициализироватьКонфигуратор();
	
	_КомандыКластер.ЗаблокироватьБазу();
	
	Лог.Информация(" Старт: Декомпиляция обработок в файлы " + ТекущаяДата());
	_КомандыКонфигуратор.РазобратьОбработки();
	Лог.Информация(" Конец: Декомпиляция обработок в файлы " + ТекущаяДата());
	
	Если _КомандыПараметры.ВернутьСвойство("LoadDB") Тогда
		Лог.Информация(" Старт: Декомпиляция конфигурации в файлы " + ТекущаяДата());
		ФайловыеОперации.ОбеспечитьПустойКаталог(_КомандыПараметры.ВернутьСвойство("PathRepository")
			+ ОбъединитьПути("\src", "cf"));
		_КомандыКонфигуратор.ВыгрузитьКонфигурациюВФайлы();
		Лог.Информация(" Конец: Декомпиляция конфигурации в файлы " + ТекущаяДата());
	КонецЕсли;
	ЗагружатьAelita = Ложь;
	ЗагружатьToolsWorld = Ложь;
	Если _КомандыПараметры.ЕстьСвойство("Extensions") Тогда
		Extensions = _КомандыПараметры.ВернутьСвойство("Extensions");
		ЗагружатьAelita = СтрНайти(Extensions, "Aelita") > 0;
		ЗагружатьToolsWorld = СтрНайти(Extensions, "ToolsWorld") > 0;
	КонецЕсли;
	
	Лог.Информация(" Старт: Декомпиляция расширений " + ТекущаяДата());
	Если Нрег(_КомандыПараметры.ВернутьСвойство("MethodLoadDB")) = "ibcmd" Тогда
		Если ЗагружатьToolsWorld Тогда
			_КомандыКонфигуратор.ВыгрузитьКонфигурациюВФайлы("ToolsWorld");
		КонецЕсли;
		Если ЗагружатьAelita Тогда
			_КомандыКонфигуратор.ВыгрузитьКонфигурациюВФайлы("Aelita");
		КонецЕсли;
	Иначе
		_КомандыКонфигуратор.ВыгрузитьРасширениеВФайлы();
	КонецЕсли;
	Лог.Информация(" Конец: Декомпиляция расширений " + ТекущаяДата());
	
	_КомандыГИТ.Закоммитить();
	
	_КомандыКластер.РазблокироватьБазу();
	
	Лог.Информация("Обработка завершена");
	
КонецПроцедуры

Функция НастройкиВыполнения()
	
	НастройкиВыполнения = Новый Структура;
	НастройкиВыполнения.Вставить("PathRepository", PathRepository);
	НастройкиВыполнения.Вставить("NameDB", NameDB);
	НастройкиВыполнения.Вставить("UsrLogin1c", UsrLogin1c);
	НастройкиВыполнения.Вставить("UsrPswd1c", UsrPswd1c);
	НастройкиВыполнения.Вставить("PathServer1с", PathServer1с);
	НастройкиВыполнения.Вставить("NumberPlatform", NumberPlatform);
	НастройкиВыполнения.Вставить("TypeDB", TypeDB);
	НастройкиВыполнения.Вставить("UsrLoginDB", UsrLoginDB);
	НастройкиВыполнения.Вставить("UsrPswdDB", UsrPswdDB);
	НастройкиВыполнения.Вставить("PathServerDB", PathServerDB);
	НастройкиВыполнения.Вставить("RunRas", RunRas);
	НастройкиВыполнения.Вставить("MethodLoadDB", MethodLoadDB);
	НастройкиВыполнения.Вставить("Extensions", Extensions);
	НастройкиВыполнения.Вставить("TextCommit", TextCommit);
	НастройкиВыполнения.Вставить("UrlRemoteRepository", UrlRemoteRepository);
	НастройкиВыполнения.Вставить("TokenConnectionGitLab", TokenConnectionGitLab);
	НастройкиВыполнения.Вставить("IdProject", IdProject);
	НастройкиВыполнения.Вставить("LoadDB", LoadDB);
	НастройкиВыполнения.Вставить("CommitAuthor", CommitAuthor);
	НастройкиВыполнения.Вставить("ServerDB", ServerDB);
	
	Возврат НастройкиВыполнения;
	
КонецФункции

#Область Инициализация

Процедура ИнициализироватьКластер(Идентификатор = "default") Экспорт
	
	_КомандыКластер = КомандыКластер;
	_КомандыКластер._КомандыПараметры = _КомандыПараметры;
	
	_КомандыКластер.Инициализировать(Идентификатор);
	
КонецПроцедуры


Процедура ИнициализироватьКонфигуратор(Идентификатор = "default") Экспорт
	
	_КомандыКонфигуратор = КомандыКонфигуратор;
	_КомандыКонфигуратор._КомандыПараметры = _КомандыПараметры;
	
	_КомандыКонфигуратор.Инициализировать(Идентификатор);
	
КонецПроцедуры

Процедура ИнициализироватьГит(Идентификатор = "default") Экспорт
	
	_КомандыГИТ = КомандыГИТ;
	_КомандыГИТ._КомандыПараметры = _КомандыПараметры;
	_КомандыГИТ.Инициализировать(Идентификатор);
	
КонецПроцедуры

Процедура ИнициализироватьНастройки(НастройкиВыполнения, ЗапускатьRas) Экспорт
	
	_КомандыПараметры = КомандыПараметры;
	_КомандыПараметры.Параметры = НастройкиВыполнения;
	
	_КомандыПараметры.ЗапускатьRas = ЗапускатьRas;
	_КомандыПараметры.Инициализировать();
	
КонецПроцедуры

#КонецОбласти