Перем _КомандыКластер;
Перем _КомандыПараметры;
Перем _КомандыКонфигуратор;
Перем _КомандыГИТ;

&Пластилин Перем КомандыГИТ Экспорт;
&Пластилин Перем КомандыКонфигуратор Экспорт;
&Пластилин Перем КомандыПараметры Экспорт;
&Пластилин Перем КомандыКластер Экспорт;
&Логер
Перем Лог;

&Опция(Имя = "PathRepository", Описание = "Путь репозитория")
Перем PathRepository;

&Опция(Имя = "NameDB", Описание = "Имя информационной базы")
Перем NameDB;

&Опция(Имя = "UsrLogin1c", Описание = "Имя пользователя 1с")
Перем UsrLogin1c;

&Опция(Имя = "UsrPswd1c", Описание = "Пароль пользователя 1с")
Перем UsrPswd1c;

&Опция(Имя = "PathServer1с", Описание = "Путь к серверу 1с")
Перем PathServer1с;

&Опция(Имя = "NumberPlatform", Описание = "Номер версии платформы 1с")
Перем NumberPlatform;

&Опция(Имя = "TypeDB", Описание = "Тип СУБД")
Перем TypeDB;

&Опция(Имя = "UsrLoginDB", Описание = "Имя пользователя СУБД")
Перем UsrLoginDB;

&Опция(Имя = "UsrPswdDB", Описание = "Пароль пользователя СУБД")
Перем UsrPswdDB;

&Опция(Имя = "PathServerDB", Описание = "Путь к серверу СУБД")
Перем PathServerDB;

&Опция(Имя = "NamesDataProcessors", Описание = "Имена внешних обработок")
Перем NamesDataProcessors;

&Опция(Имя = "RunRas", Описание = "Запускать RAS")
Перем RunRas;

&Опция(Имя = "MethodLoadDB", Описание = "ibcmd / designer")
Перем MethodLoadDB;

&Опция(Имя = "NamesUpdateDB", Описание = "Пример st-serv-dev-01$TEST_WMS83ST_LDC$user$password, st-serv-dev-01$TEST_WMS83ST_KDO$user$password")
Перем NamesUpdateDB;

&КомандаПриложения(Имя = "UpdateDataProcessors")
Процедура ПриСозданииОбъекта()
		
КонецПроцедуры

&ВыполнениеКоманды
Процедура ОбработатьКоманду() Экспорт

	НастройкиВыполнения = НастройкиВыполнения();
	
	ИнициализироватьНастройки(НастройкиВыполнения, RunRas);
	ИнициализироватьКластер();
	ИнициализироватьГит();
	ИнициализироватьКонфигуратор();

	_КомандыГИТ.ПерейтиНаВеткуМейн();

	ПутьОбработокРепо = PathRepository + "\build\epf";
	файловыеОперации.ОбеспечитьПустойКаталог(ПутьОбработокРепо);
	
	
	//Скомпилировтаь обработки
	Лог.Информация(" Старт: Компиляция обработок " + ТекущаяДата());
	_КомандыКонфигуратор.СобратьОбработки();
	Лог.Информация(" Конец: Компиляция обработок " + ТекущаяДата());

	//_КомандыКластер.РазблокироватьБазу();

	ДанныеОбновляемыхБД = ДанныеОбновляемыхБД(NamesUpdateDB);
	Для Каждого ОбновляемаяБД Из ДанныеОбновляемыхБД Цикл
		
		Лог.Информация(" Старт: Перенос обработок в базу рабочую " + ОбновляемаяБД.База + " " + ТекущаяДата());

		ПубликацияОбработокВБазе.ОбновитьОбработкиВБазе(ПутьОбработокРепо, ОбновляемаяБД.Сервер,
			ОбновляемаяБД.База, ОбновляемаяБД.Пользователь, ОбновляемаяБД.Пароль);

		Лог.Информация(" Конец: Перенос обработок в базу рабочую " + ОбновляемаяБД.База + " " + ТекущаяДата());
	
	КонецЦикла;			

	Лог.Информация("Обработка завершена");
	
КонецПроцедуры

Функция НовыйДанныеОбновляемыхБД()

	ДанныеОбновляемыхБД = Новый ТаблицаЗначений;
	ДанныеОбновляемыхБД.Колонки.Добавить("Сервер");
	ДанныеОбновляемыхБД.Колонки.Добавить("База");
	ДанныеОбновляемыхБД.Колонки.Добавить("Пользователь");
	ДанныеОбновляемыхБД.Колонки.Добавить("Пароль");

	Возврат ДанныеОбновляемыхБД;

КонецФункции

Функция ДанныеОбновляемыхБД(NamesUpdateDB)
	
	ДанныеОбновляемыхБД = НовыйДанныеОбновляемыхБД();

	КонфигурацииСИменемСервера = СтрРазделить(NamesUpdateDB, ",");
	Для Каждого КонфигурацияСИменемСервера Из КонфигурацииСИменемСервера Цикл
		
		КонфигурацияРазделенная = СтрРазделить(КонфигурацияСИменемСервера, "$");
		
		СтрокаДанных = ДанныеОбновляемыхБД.Добавить();
		СтрокаДанных.Сервер = СокрЛП(КонфигурацияРазделенная[0]);
		СтрокаДанных.База = СокрЛП(КонфигурацияРазделенная[1]);
		СтрокаДанных.Пользователь = СокрЛП(КонфигурацияРазделенная[2]);
		СтрокаДанных.Пароль = СокрЛП(КонфигурацияРазделенная[3]);

	КонецЦикла;

	Возврат ДанныеОбновляемыхБД;

КонецФункции

Функция НастройкиВыполнения()

	НастройкиВыполнения = Новый Структура;
	НастройкиВыполнения.Вставить("PathRepository", PathRepository);
	НастройкиВыполнения.Вставить("NameDB", NameDB);
	НастройкиВыполнения.Вставить("UsrLogin1c", UsrLogin1c);
	НастройкиВыполнения.Вставить("UsrPswd1c", UsrPswd1c);
	НастройкиВыполнения.Вставить("PathServer1с", PathServer1с);
	НастройкиВыполнения.Вставить("NumberPlatform", NumberPlatform);
	НастройкиВыполнения.Вставить("TypeDB", TypeDB);
	НастройкиВыполнения.Вставить("UsrLoginDB", UsrLoginDB);
	НастройкиВыполнения.Вставить("UsrPswdDB", UsrPswdDB);
	НастройкиВыполнения.Вставить("PathServerDB", PathServerDB);
	НастройкиВыполнения.Вставить("RunRas", RunRas);
	НастройкиВыполнения.Вставить("MethodLoadDB", MethodLoadDB);
	НастройкиВыполнения.Вставить("NamesDataProcessors", NamesDataProcessors);

	Возврат НастройкиВыполнения;

КонецФункции

#Область Инициализация

Процедура ИнициализироватьКластер(Идентификатор = "default") Экспорт
	
	_КомандыКластер = КомандыКластер;
	_КомандыКластер._КомандыПараметры = _КомандыПараметры;

	_КомандыКластер.Инициализировать(Идентификатор);
	
КонецПроцедуры


Процедура ИнициализироватьКонфигуратор(Идентификатор = "default") Экспорт
	
	_КомандыКонфигуратор = КомандыКонфигуратор;
	_КомандыКонфигуратор._КомандыПараметры = _КомандыПараметры;

	_КомандыКонфигуратор.Инициализировать(Идентификатор);
	
КонецПроцедуры

Процедура ИнициализироватьГит(Идентификатор = "default") Экспорт
	
	_КомандыГИТ = КомандыГИТ;
	_КомандыГИТ._КомандыПараметры = _КомандыПараметры;
	_КомандыГИТ.Инициализировать(Идентификатор);

КонецПроцедуры

Процедура ИнициализироватьНастройки(НастройкиВыполнения, ЗапускатьRas) Экспорт
	
	_КомандыПараметры = КомандыПараметры;
	_КомандыПараметры.Параметры = НастройкиВыполнения;

	_КомандыПараметры.ЗапускатьRas = ЗапускатьRas;
	_КомандыПараметры.Инициализировать();
	
КонецПроцедуры

#КонецОбласти