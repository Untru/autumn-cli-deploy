Перем _КомандыПараметры;
Перем _КомандыКонфигуратор;
Перем _КомандыГИТ;

&Пластилин Перем КомандыГИТ Экспорт;
&Пластилин Перем КомандыКонфигуратор Экспорт;
&Пластилин Перем КомандыПараметры Экспорт;
&Пластилин Перем КомандыКластер Экспорт;
&Логер
Перем Лог;

&Опция(Имя = "PathRepository", Описание = "Путь репозитория")
Перем PathRepository;

&Опция(Имя = "PathRepositoryMain", Описание = "Путь репозитория основная папка")
Перем PathRepositoryMain;

&Опция(Имя = "NamesDataProcessors", Описание = "Имя внешних обработок")
Перем NamesDataProcessors;

&Опция(Имя = "CommitAuthor", Описание = "Автор, от которого отправляется коммит")
Перем CommitAuthor;

&КомандаПриложения(Имя = "MakeDataProcessors")
Процедура ПриСозданииОбъекта()
	
КонецПроцедуры

&ВыполнениеКоманды
Процедура ОбработатьКоманду() Экспорт
	
	НастройкиВыполнения = НастройкиВыполнения();
	
	ИнициализироватьНастройки(НастройкиВыполнения);
	ИнициализироватьГит();
	ИнициализироватьКонфигуратор();
	
	//Скомпилироватаь обработку
	Лог.Информация(" Старт: Компиляция обработки " + ТекущаяДата());
	КаталогШаблон = "Шаблоны";
	КаталогОбработки = "src\epf";
	ИмяШаблонаВБЛ = "ШаблонРеализацииОбработкиВРЗилиВБЛ";
	ПапкаСОбработками = "epf";

	файловыеОперации.КопироватьСодержимоеКаталогаССозданием(КаталогШаблон, КаталогОбработки, NamesDataProcessors);
	
	ПутьКXMLНовойОбработки = СтрШаблон("%1\%2.xml", КаталогОбработки, NamesDataProcessors);
	
	МассивФайлов = Новый Массив();
	МассивФайлов.Добавить(ПутьКXMLНовойОбработки);
	МассивФайлов.Добавить(СтрШаблон("%1\%2\Forms\ФормаЧегоЛибо\Ext\Form.xml", КаталогОбработки, NamesDataProcessors));
	МассивФайлов.Добавить(СтрШаблон("%1\%2\Forms\ФормаНастроек\Ext\Form.xml", КаталогОбработки, NamesDataProcessors));
	
	СоответствиеСтрокЗамен = Новый Соответствие;
	СоответствиеСтрокЗамен.Вставить(ИмяШаблонаВБЛ, NamesDataProcessors);
	
	Для Каждого Элемент Из МассивФайлов Цикл
		РаботаСМодулями.ЗаменаСтрокМодуля(Элемент, СоответствиеСтрокЗамен);
	КонецЦикла;

	_КомандыКонфигуратор.КомпилироватьФайл(ПутьКXMLНовойОбработки, NamesDataProcessors);
	Лог.Информация(" Конец: Компиляция обработок " + ТекущаяДата());
	_КомандыГИТ.Закоммитить();
	Лог.Информация("Обработка завершена");
	
КонецПроцедуры

Функция НастройкиВыполнения()
	
	НастройкиВыполнения = Новый Структура;
	НастройкиВыполнения.Вставить("PathRepository", PathRepository);
	НастройкиВыполнения.Вставить("PathRepositoryMain", PathRepositoryMain);
	Лог.Информация("Инициализация" + PathRepositoryMain);
	НастройкиВыполнения.Вставить("NamesDataProcessors", NamesDataProcessors);
	НастройкиВыполнения.Вставить("CommitAuthor", CommitAuthor);
	НастройкиВыполнения.Вставить("TextCommit", "Добавление обработок");
		
	Возврат НастройкиВыполнения;
	
КонецФункции

#Область Инициализация

Процедура ИнициализироватьКонфигуратор(Идентификатор = "default") Экспорт
	
	_КомандыКонфигуратор = КомандыКонфигуратор.Достать();
	_КомандыКонфигуратор._КомандыПараметры = _КомандыПараметры;
	
	_КомандыКонфигуратор.ИнициализироватьДляФайловойБазы(Идентификатор);
	
КонецПроцедуры

Процедура ИнициализироватьГит(Идентификатор = "default") Экспорт
	
	_КомандыГИТ = КомандыГИТ.Достать();
	_КомандыГИТ._КомандыПараметры = _КомандыПараметры;
	_КомандыГИТ.Инициализировать(Идентификатор);
	
КонецПроцедуры

Процедура ИнициализироватьНастройки(НастройкиВыполнения) Экспорт
	
	_КомандыПараметры = КомандыПараметры.Достать();
	_КомандыПараметры.Параметры = НастройкиВыполнения;
	_КомандыПараметры.Инициализировать();
	
КонецПроцедуры

#КонецОбласти