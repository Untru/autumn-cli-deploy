#Использовать ".."
#Использовать asserts
#Использовать tempfiles

Перем юТест;
Перем УправлениеКонфигуратором;
Перем Лог;
Перем мВременнаяВыгрузка;
Перем СуффиксКТестам;

Процедура Инициализация()
	
	УправлениеКонфигуратором = Новый УправлениеКонфигуратором;
	
	Лог = Логирование.ПолучитьЛог("oscript.lib.v8runner");
	Лог.УстановитьУровень(УровниЛога.Отладка);

	СуффиксКТестам = "";
	Попытка
		УправлениеКонфигуратором.ИспользоватьВерсиюПлатформы("8.3.10");
	Исключение
		СуффиксКТестам = "Не_Найдена_Платформа_8_3_10_или_старше";;
	КонецПопытки;

КонецПроцедуры

Функция ПолучитьСписокТестов(Тестирование) Экспорт
	
	юТест = Тестирование;
	
	ВремСписокТестов = Новый Массив;
		
	ВремСписокТестов.Добавить("ТестДолжен_ВыгрузитьКонфигурациюВФайлы");
    ВремСписокТестов.Добавить("ТестДолжен_ВыгрузитьИзмененияКонфигурацииВФайл");
		
	ВремСписокТестов.Добавить("ТестДолжен_ВыгрузитьКонфигурациюВФайлыОтносительноФайлаВерсий");
    ВремСписокТестов.Добавить("ТестДолжен_ВыгрузитьИзмененияКонфигурацииВФайлОтносительноФайлаВерсий");
    
	ВремСписокТестов.Добавить("ТестДолжен_ЗагрузитьКонфигурациюИзФайлов");


	ВремСписокТестов.Добавить("ТестДолжен_ВыгрузитьРасширениеВФайлы");
	ВремСписокТестов.Добавить("ТестДолжен_ВыгрузитьВсеРасширенияВФайлы");
    ВремСписокТестов.Добавить("ТестДолжен_ВыгрузитьИзмененияРасширенияВФайл");
		
	ВремСписокТестов.Добавить("ТестДолжен_ВыгрузитьРасширениеВФайлыОтносительноФайлаВерсий");
    ВремСписокТестов.Добавить("ТестДолжен_ВыгрузитьИзмененияРасширенияВФайлОтносительноФайлаВерсий");
    
	ВремСписокТестов.Добавить("ТестДолжен_ЗагрузитьРасширениеИзФайлов");


	СписокТестов = Новый Массив;
	Для каждого ИмяТеста Из ВремСписокТестов Цикл
		СписокТестов.Добавить(ИмяТеста + СуффиксКТестам);
	КонецЦикла;

	Возврат СписокТестов;
	
КонецФункции

Процедура ТестДолжен_ВыгрузитьКонфигурациюВФайлы() Экспорт

	ПодготовитьВременнуюВыгрузку();

	ВыгрузитьКонфигурациюВФайлы("1.0", мВременнаяВыгрузка);

	ФайлВерсииКонфигурации = Новый Файл(ОбъединитьПути(мВременнаяВыгрузка, "ConfigDumpInfo.xml"));
	
	МассивФайловВыгрузки = НайтиФайлы(мВременнаяВыгрузка,"*", Истина);
	
	Утверждения.ПроверитьРавенство(6, МассивФайловВыгрузки.Количество(), "Неверное количество файлов выгрузки. Ожидали число слева, а получили другое число выгруженных файлов");
	Утверждения.ПроверитьИстину(ФайлВерсииКонфигурации.Существует(), "Файл версии кофигурации должен существовать");

КонецПроцедуры

Процедура ТестДолжен_ВыгрузитьРасширениеВФайлы() Экспорт

	ИмяРасширения = "Расширение1";
	ПодготовитьВременнуюВыгрузку(ИмяРасширения);

	ВыгрузитьКонфигурациюВФайлы("1.0", мВременнаяВыгрузка, ИмяРасширения);

	ФайлВерсииКонфигурации = Новый Файл(ОбъединитьПути(мВременнаяВыгрузка, "ConfigDumpInfo.xml"));
	
	МассивФайловВыгрузки = НайтиФайлы(мВременнаяВыгрузка,"*", Истина);
	
	Утверждения.ПроверитьРавенство(6, МассивФайловВыгрузки.Количество(), "Неверное количество файлов выгрузки. Ожидали число слева, а получили другое число выгруженных файлов");
	Утверждения.ПроверитьИстину(ФайлВерсииКонфигурации.Существует(), "Файл версии кофигурации должен существовать");

КонецПроцедуры

Процедура ТестДолжен_ВыгрузитьВсеРасширенияВФайлы() Экспорт

	ИмяРасширения = "Расширение1";
	ПодготовитьВременнуюВыгрузку();

	ПутьФайлКонфигурацииРасширения = ОбъединитьПути(ТекущийСценарий().Каталог, "fixtures", "1.0", "1Cv8.cfe");
	УправлениеКонфигуратором.ЗагрузитьРасширениеИзФайла(ПутьФайлКонфигурацииРасширения, ИмяРасширения);
	УправлениеКонфигуратором.ВыгрузитьРасширениеВФайлы(мВременнаяВыгрузка);
	
	КаталогРасширения = Новый Файл(ОбъединитьПути(мВременнаяВыгрузка, ИмяРасширения));
	Утверждения.ПроверитьИстину(КаталогРасширения.Существует() И КаталогРасширения.ЭтоКаталог(), "Расширение должно было быть выгружено в отдельный одноименный каталог");
	
	ФайлВерсииКонфигурации = Новый Файл(ОбъединитьПути(мВременнаяВыгрузка, ИмяРасширения, "ConfigDumpInfo.xml"));
	Утверждения.ПроверитьИстину(ФайлВерсииКонфигурации.Существует(), "Файл версии кофигурации должен существовать");

КонецПроцедуры

Процедура ПодготовитьВременнуюВыгрузку(Знач ИмяРасширения = "")
	
	Если ЗначениеЗаполнено(мВременнаяВыгрузка) Тогда
		Возврат;
	КонецЕсли; 

	ВременныйКаталог = ВременныеФайлы.СоздатьКаталог();

	УправлениеКонфигуратором.КаталогСборки(ВременныйКаталог);
	
	КаталогВыгрузки = ОбъединитьПути(ВременныйКаталог, "v8r_TempDitr");

	ВыгрузитьКонфигурациюВФайлы("0.9", КаталогВыгрузки,ИмяРасширения);

	мВременнаяВыгрузка = КаталогВыгрузки;
	
КонецПроцедуры

Процедура ВыгрузитьКонфигурациюВФайлы(Знач Версия, Знач КаталогВыгрузки, Знач ИмяРасширения = "")
	
	ПутьФайлКонфигурации = ОбъединитьПути(ТекущийСценарий().Каталог, "fixtures", Версия, "1Cv8.cf");
	
	УправлениеКонфигуратором.ЗагрузитьКонфигурациюИзФайла(ПутьФайлКонфигурации);
	Если ПустаяСтрока(ИмяРасширения) Тогда
		УправлениеКонфигуратором.ВыгрузитьКонфигурациюВФайлы(КаталогВыгрузки);
	Иначе
		ПутьФайлКонфигурацииРасширения = ОбъединитьПути(ТекущийСценарий().Каталог, "fixtures", Версия, "1Cv8.cfe");
		УправлениеКонфигуратором.ЗагрузитьРасширениеИзФайла(ПутьФайлКонфигурацииРасширения,ИмяРасширения);
		УправлениеКонфигуратором.ВыгрузитьРасширениеВФайлы(КаталогВыгрузки, ИмяРасширения);
	КонецЕсли;

КонецПроцедуры

Инициализация();
