#Использовать "../src/MyLib/v8runner"
#Использовать "../src/MyLib/ibcmdrunner"
#Использовать logos

Перем Конфигуратор Экспорт;
Перем УправлениеИБ Экспорт;
Перем _КомандыПараметры Экспорт;

&Характер("Компанейский")
&Желудь
Процедура ПриСозданииОбъекта()
	
	
КонецПроцедуры

Процедура Инициализировать(Идентификатор) Экспорт
	
	Конфигуратор = Новый МойУправлениеКонфигуратором;
	Конфигуратор.ИдентификаторЛога = Идентификатор;
	
	УправлениеИБ = Новый МойУправлениеИБ;
	УправлениеИБ.ИдентификаторЛога = Идентификатор;
	УправлениеИБ.ПодменитьИмяЛога(Идентификатор); 
	
	УправлениеИБ.ПутьКПриложению("C:\Program Files\1cv8\8.3.22.2283\bin\ibcmd.exe");
	
	Конфигуратор.УстановитьКлючРазрешенияЗапуска("Обновление");
	
	ИмяФайлаOut = СтрШаблон("%1\build\out.txt", _КомандыПараметры.ВернутьСвойство("PathRepository"));
	Конфигуратор.УстановитьИмяФайлаСообщенийПлатформы(ИмяФайлаOut);
	
	ПараметрыСтрокиСоединения = Конфигуратор.ПараметрыСтрокиСоединения();
	ПараметрыСтрокиСоединения.Сервер = _КомандыПараметры.ВернутьСвойство("PathServer1с");
	
	ПараметрыСтрокиСоединения.Порт = 1541;
	ПараметрыСтрокиСоединения.ИмяБазы = _КомандыПараметры.ВернутьСвойство("NameDB");
	
	Конфигуратор.УстановитьКонтекст(ПараметрыСтрокиСоединения
		, _КомандыПараметры.ВернутьСвойство("UsrLogin1c"), _КомандыПараметры.ВернутьСвойство("UsrPswd1c"));
	
	Конфигуратор.ИспользоватьВерсиюПлатформы(_КомандыПараметры.ВернутьСвойство("NumberPlatform"));
	
	УправлениеИБ.УстановитьПараметрыСервернойИБ(_КомандыПараметры.ВернутьСвойство("TypeDB")
		, _КомандыПараметры.ВернутьСвойство("PathServer1с")
		, _КомандыПараметры.ВернутьСвойство("NameDB")
		, _КомандыПараметры.ВернутьСвойство("UsrLoginDB")
		, _КомандыПараметры.ВернутьСвойство("UsrPswdDB"));
	
	УправлениеИБ.УстановитьПараметрыАвторизацииИБ(_КомандыПараметры.ВернутьСвойство("UsrLogin1c")
		, _КомандыПараметры.ВернутьСвойство("UsrPswd1c"));
	
КонецПроцедуры

Процедура ОбновитьБазуИзФайлов() Экспорт

	Если Нрег(_КомандыПараметры.ВернутьСвойство("MethodLoadDB")) = "ibcmd" Тогда
		ЧислоПопыток = 3;
		Для Сч = 1 По ЧислоПопыток Цикл
			УправлениеИБ.ЗагрузитьКонфигурациюИзФайлов(СтрШаблон("%1\src\cf", _КомандыПараметры.ВернутьСвойство("PathRepository")));
			Если Не УправлениеИБ.УспешноеВыполнениеКоманды Тогда
				Сообщить("Ошибка при выполнении команды ibcmd");
			Иначе
				Прервать;
			КонецЕсли;		
		КонецЦикла;

		Если Не УправлениеИБ.УспешноеВыполнениеКоманды Тогда
			ВызватьИсключение "Исчерпано количество попыток ibcmd. Попробуйте заново";
		КонецЕсли; 

		ЧислоПопыток = 3;
		Для Сч = 1 По ЧислоПопыток Цикл
			УправлениеИБ.ОбновитьКонфигурациюБазыДанных();
			Если Не УправлениеИБ.УспешноеВыполнениеКоманды Тогда
				Сообщить("Ошибка при выполнении команды ibcmd");
			Иначе
				Прервать;				
			КонецЕсли;

		КонецЦикла;

		Если Не УправлениеИБ.УспешноеВыполнениеКоманды Тогда
			ВызватьИсключение "Исчерпано количество попыток ibcmd. Попробуйте заново";
		КонецЕсли; 

	Иначе
		Конфигуратор.ЗагрузитьКонфигурациюИзФайлов(СтрШаблон("%1\src\cf", _КомандыПараметры.ВернутьСвойство("PathRepository")),,,, Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗагрузитьРасширенияИзФайлов() Экспорт

	Конфигуратор.ЗагрузитьРасширениеИзФайлов(СтрШаблон("%1\src\cfe", _КомандыПараметры.ВернутьСвойство("PathRepository")));
	Конфигуратор.ОбновитьКонфигурациюБазыДанных(,,, "ToolsWorld");

КонецПроцедуры

Процедура ВыгрузитьРасширениеВФайлы() Экспорт

	Конфигуратор.ВыгрузитьРасширениеВФайлы(СтрШаблон("%1\src\cfe", _КомандыПараметры.ВернутьСвойство("PathRepository")));
	
КонецПроцедуры

Процедура ВыгрузитьКонфигурациюВФайлы() Экспорт
	
	Если Нрег(_КомандыПараметры.ВернутьСвойство("MethodLoadDB")) = "ibcmd" Тогда
		
		ЧислоПопыток = 3;
		Для Сч = 1 По ЧислоПопыток Цикл
			УправлениеИБ.ВыгрузитьКонфигурациюВФайлы(СтрШаблон("%1\src\cf", _КомандыПараметры.ВернутьСвойство("PathRepository")), "", "", Ложь);
			Если Не УправлениеИБ.УспешноеВыполнениеКоманды Тогда
				Сообщить("Ошибка при выполнении команды ibcmd");
			Иначе
				Прервать;				
			КонецЕсли;

		КонецЦикла;

		Если Не УправлениеИБ.УспешноеВыполнениеКоманды Тогда
			ВызватьИсключение "Исчерпано количество попыток ibcmd. Попробуйте заново";
		КонецЕсли; 		

	Иначе
		Конфигуратор.ВыгрузитьКонфигурациюВФайлы(СтрШаблон("%1\src\cf", _КомандыПараметры.ВернутьСвойство("PathRepository")));
	КонецЕсли;

КонецПроцедуры

Процедура ОбновитьОбработкиВКонфигурации() Экспорт
	ПараметрЗапуска = "ОбновлениеОбработок#" + _КомандыПараметры.ВернутьСвойство("PathRepository") + "\build\epf";
	Конфигуратор.ЗапуститьВРежимеПредприятия(ПараметрЗапуска);
КонецПроцедуры

Процедура СобратьОбработки() Экспорт
	
	Путь = _КомандыПараметры.ВернутьСвойство("PathRepository") + "\src\epf";
	Маска = "*.xml";

	ИменаОбработок = СтрРазделить(_КомандыПараметры.ВернутьСвойство("NamesDataProcessors"), ",");

	Для Каждого ИмяОбработки Из ИменаОбработок Цикл			
		ПолноеИмя = СтрШаблон("%1\%2\%3.xml", _КомандыПараметры.ВернутьСвойство("PathRepository"), "\src\epf", ИмяОбработки);
		КомпилироватьФайл(ПолноеИмя, ИмяОбработки);		
	КонецЦикла;	

	записьТекста = Новый ЗаписьТекста("build\ДатаЗагрузки.txt");
	ЗаписьТекста.ЗаписатьСтроку(ТекущаяДата());
	ЗаписьТекста.Закрыть();
	
КонецПроцедуры

Процедура КомпилироватьФайл(ИмяФайл, ИмяОбработки)
	//Путь куда мы собираем готовые
	КаталогСобранныхОбработок = _КомандыПараметры.ВернутьСвойство("PathRepository") + СтрШаблон("\build\epf\");
	
	ПараметрыЗапуска = Конфигуратор.ПолучитьПараметрыЗапуска();
	ПараметрыЗапуска.Добавить("/LoadExternalDataProcessorOrReportFromFiles """
		+ ИмяФайл + """  """ + КаталогСобранныхОбработок + ИмяОбработки + """");
	
	Попытка
		Конфигуратор.ВыполнитьКоманду(ПараметрыЗапуска);
	Исключение
		Сообщить("Ошибка с файлом" + ИмяОбработки);
		//Если есть ошибка, потом с разберемся с этим файлом, но не будем прерывать работу.
		ВызватьИсключение "Выгрузка обработок в xml прервана.";
	КонецПопытки;
	
КонецПроцедуры

Процедура РазобратьОбработки() Экспорт
	
	Файл = Новый Файл(_КомандыПараметры.ВернутьСвойство("PathRepository") + "\build\ДатаЗагрузки.txt");
	ВремяИзменения = Файл.ПолучитьВремяИзменения();
	
	Путь = _КомандыПараметры.ВернутьСвойство("PathRepository") + "\build\epf";
	Маска = "*.e?f";
	
	Файлы = НайтиФайлы(Путь, Маска, Ложь);
	
	НомерФайла = 0;
	Для Каждого Файл Из Файлы Цикл
		
		ВремяИзмененияОбработки = Файл.ПолучитьВремяИзменения();
		Если Файл.ПолучитьВремяИзменения() > ВремяИзменения Тогда
			ВыгрузитьФайлВXML(Файл.ПолноеИмя);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВыгрузитьФайлВXML(ИмяФайла)
	
	//Путь к исходникам
	КаталогОбработок = _КомандыПараметры.ВернутьСвойство("PathRepository") + СтрШаблон("\src\%1", "epf");
	
	ПараметрыЗапуска = Конфигуратор.ПолучитьПараметрыЗапуска();
	ПараметрыЗапуска.Добавить("/DumpExternalDataProcessorOrReportToFiles """ + КаталогОбработок + """  """ + ИмяФайла + """");
	
	Попытка
		Конфигуратор.ВыполнитьКоманду(ПараметрыЗапуска);
	Исключение
		ВызватьИсключение "Выгрузка обработок в xml прервана.";
	КонецПопытки;
	
КонецПроцедуры

Процедура СобратьОбработкиЧерезУправлениеИБ() Экспорт
	
	
	
КонецПроцедуры